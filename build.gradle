buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
	}
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'

jar {
	baseName = 'timneh'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

// apply the runtimeClasspath from "test" sourceSet to the new one
// to include any needed assets: test, main, test-dependencies and main-dependencies
sourceSets {
	integrationTest {
		compileClasspath += sourceSets.test.runtimeClasspath
		// somehow this redeclaration is needed, but should be irrelevant
		// since runtimeClasspath always expands compileClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
	}
}

// define custom test task for running integration tests
task integrationTest(type: Test) {
	description = "Runs Integration Tests"
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	dependsOn processResources
	dependsOn test
}

tasks.withType(Test) {
	systemProperties = System.getProperties()
}

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	compile     'org.codehaus.groovy:groovy-all:2.4.7'
	compile     'org.springframework.boot:spring-boot-starter-actuator'
	compile     'org.springframework.boot:spring-boot-starter-jetty'
	compile     'org.springframework.boot:spring-boot-starter-jdbc'
	compile     'org.springframework.boot:spring-boot-starter-security'
	compile     'io.jsonwebtoken:jjwt:0.7.0'
	runtime     'org.webjars:angularjs:1.5.8'
	runtime     'org.webjars:bootstrap:3.3.7-1'
	runtime     'org.webjars:webjars-locator:0.32'
	runtime     'org.postgresql:postgresql:9.4.1211'
	runtime     'org.hsqldb:hsqldb:2.3.4'
	testCompile 'cglib:cglib-nodep:3.2.4' // Allow mocking of non-interface types
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-2'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.security:spring-security-test'
	testCompile 'org.apache.httpcomponents:httpclient'
}

bootRun {
	addResources = true // Reload static resources live
}
bootRun.systemProperties = System.properties

processResources.doLast {
	// If we're using HSQLDB (i.e., no Spring profile specified), prepend the
	// HSQLDB setup commands before the main DDL statements.
	if (!System.getProperty('spring.profiles.active')) {
		(new File("$buildDir/resources/main/schema.sql")).text =
				file("src/main/resources/header-hsqldb.sql").getText() +
				file("src/main/resources/schema-postgresql-from-scratch.sql").getText()
		(new File("$buildDir/resources/main/data.sql")).text =
				file("src/main/resources/header-hsqldb.sql").getText() +
				file("src/main/resources/data-postgresql-from-scratch.sql").getText()
	}
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

check.dependsOn clean // Clean profile-specific DDL we may have generated
check.dependsOn integrationTest
check.dependsOn jacocoTestReport // Code coverage
