buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.1.RELEASE'
	}
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'

jar {
	baseName = 'timneh'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

// apply the runtimeClasspath from "test" sourceSet to the new one
// to include any needed assets: test, main, test-dependencies and main-dependencies
sourceSets {
	integrationTest {
		compileClasspath += sourceSets.test.runtimeClasspath
		// somehow this redeclaration is needed, but should be irrelevant
		// since runtimeClasspath always expands compileClasspath
		runtimeClasspath += sourceSets.test.runtimeClasspath
	}
}

// define custom test task for running integration tests
task integrationTest(type: Test) {
	description = "Runs Integration Tests"
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
}
integrationTest.dependsOn test

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
	}
	compile     'org.codehaus.groovy:groovy-all:2.4.7'
	compile     'org.springframework.boot:spring-boot-starter-actuator'
	compile     'org.springframework.boot:spring-boot-starter-jetty'
	compile     'org.springframework.boot:spring-boot-starter-jdbc'
	compile     'org.springframework.boot:spring-boot-starter-security'
	runtime     'org.webjars:angularjs:1.5.8'
	runtime     'org.webjars:bootstrap:3.3.4'
	runtime     'org.webjars:webjars-locator:0.32'
	runtime     'org.postgresql:postgresql:9.4.1211.jre7'
	runtime     'org.hsqldb:hsqldb:2.3.4'
	testCompile 'org.spockframework:spock-core:1.1-groovy-2.4-rc-2'
	testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4-rc-2'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework.security:spring-security-test'
}

bootRun {
	addResources = true // Reload static resources live
	systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
}

